# zeesh zsh autocomplete plugin

HOMEBREW_ZSH_COMPLETION=${HOMEBREW_ZSH_COMPLETION:-/usr/local/Library/Contributions/brew_zsh_completion.zsh}

# symlink _brew if on osx
if [ ! -f "$plugin_dir/comp/_brew" ] && [ -f $HOMEBREW_ZSH_COMPLETION ]; then
    ln -s $HOMEBREW_ZSH_COMPLETION $plugin_dir/comp/_brew
fi

# add ellipsis, zeesh comps to $fpath
fpath=( $plugin_dir/comp $HOME/.ellipsis/comp $fpath )

# enable autocompletions and cache in ~/.zsh/cache
autoload -Uz compinit && compinit -i -u -d ~/.zsh/cache/zcompdump

# speed up completion
zstyle ':completion:*' accept-exact '*(N)'
zstyle ':completion:*' cache-path ~/.zsh/cache
zstyle ':completion:*' use-cache on

zstyle ':completion:*' completer _complete _ignored _match _correct _approximate _prefix

zstyle ':completion:*:*:open:*' matcher 'm:{a-z}={A-Z} r: ||[^ ]=**'

# case-insensitive (all), partial-word and then substring completion
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' list-colors ''
zstyle ':completion:*:*:*:*:*' menu select
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;34=0=01'

zstyle ':completion:*:*:*:users' ignored-patterns \
        Guest adm amanda apache avahi beaglidx bin cacti canna clamav daemon \
        dbus distcache dovecot fax ftp games gdm gkrellmd gopher hacluster \
        haldaemon halt hsqldb ident junkbust ldap lp mail mailman mailnull \
        mldonkey mysql nagios named netdump news nfsnobody nobody nscd ntp \
        nut nx openvpn operator pcap postfix postgres privoxy pulse pvm \
        quagga radvd rpc rpcuser rpm shutdown squid sshd sync uucp vcsa xfs '_*'

# ... unless we really want to.
zstyle '*' single-ignored show

# Ignore multiple entries.
zstyle ':completion:*:(rm|kill|diff):*' ignore-line yes
zstyle ':completion:*:rm:*' file-patterns '*:all-files'

# SSH/SCP/RSYNC
zstyle ':completion:*:(scp|rsync):*' tag-order 'hosts:-host hosts:-domain:domain hosts:-ipaddr:ip\ address *'
zstyle ':completion:*:(scp|rsync):*' group-order users files all-files hosts-domain hosts-host hosts-ipaddr
zstyle ':completion:*:ssh:*' tag-order 'hosts:-host hosts:-domain:domain hosts:-ipaddr:ip\ address *'
zstyle ':completion:*:ssh:*' group-order hosts-domain hosts-host users hosts-ipaddr
zstyle ':completion:*:(ssh|scp|rsync):*:hosts-host' ignored-patterns '*.*' loopback localhost
zstyle ':completion:*:(ssh|scp|rsync):*:hosts-domain' ignored-patterns '<->.<->.<->.<->' '^*.*' '*@*'
zstyle ':completion:*:(ssh|scp|rsync):*:hosts-ipaddr' ignored-patterns '^<->.<->.<->.<->' '127.0.0.<->'

# hide shit
zstyle ':completion:*:*' ignored-patterns '*.zwc' '*.old' 'broadcasthost' '*?.pyc'

# zshfu plugin list autocomplete
compctl -K zeesh-plugin-list zeesh-plugin

COMP_WORDBREAKS=${COMP_WORDBREAKS/=/}
COMP_WORDBREAKS=${COMP_WORDBREAKS/@/}
export COMP_WORDBREAKS

_npm_completion () {
    local cword line point words si
    read -Ac words
    read -cn cword
    let cword-=1
    read -l line
    read -ln point
    si="$IFS"
    IFS=$'\n' reply=($(COMP_CWORD="$cword" \
                       COMP_LINE="$line" \
                       COMP_POINT="$point" \
                       npm completion -- "${words[@]}" \
                       2>/dev/null)) || return $?
    IFS="$si"
}
compctl -K _npm_completion npm

# Add my manage.py completion hack.
compdef _managepy manage.py

# vim: ft=zsh
